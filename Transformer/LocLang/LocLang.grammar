using System.Collections.Generic;
using Prem.Util;
using Prem.Transformer.LocLang;
using Microsoft.ProgramSynthesis.Utils;

using semantics Prem.Transformer.LocLang.Semantics;
using learners Prem.Transformer.LocLang.WitnessFunctions;

language LocLang;

@complete feature double Score = Prem.Transformer.LocLang.RankingScore;

@input SyntaxNode source;

@start SyntaxNode program := Insert(ref, k, tree) | Delete(ref) | Update(ref, tree);

SyntaxNode tree := ref | Node(label, children);

IEnumerable<SyntaxNode> children := Child(tree) | Children(tree, children);

SyntaxNode ref := Ref(candidates, k) = Kth(candidates, k);

IEnumerable<SyntaxNode> candidates := Find(constraint, subtrees)
                                    = Filter(\x: SyntaxNode => constraint, subtrees);

IEnumerable<SyntaxNode> subtrees := Sub(ancestor);

Node ancestor := AbsAncestor(source, k) | RelAncestor(source, labelK);

Record<string, int>? labelK := LabelK(label, k) = Pair(label, k);

bool constraint := Any(x)
                 | AnyError(x)
                 | AnyToken(x) | TokenMatch(x, type)
                 | AnyNode(x) | NodeMatch(x, label);

int k;
string label;
int type;