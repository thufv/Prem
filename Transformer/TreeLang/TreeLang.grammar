using System.Collections.Generic;
using Microsoft.ProgramSynthesis.Utils;

using Prem.Util;
using Prem.Transformer.TreeLang;

using semantics Prem.Transformer.TreeLang.Semantics;
using learners Prem.Transformer.TreeLang.PremStrategy;

language TreeLang;

@complete feature double Score = Prem.Transformer.TreeLang.RankingScore;

@input TInput old;

@start SyntaxNode program := Transform(target, newTree);

SyntaxNode target := source | ref;

SyntaxNode source := Err(old) | Var(old, key);

SyntaxNode ref := Select(scope, index) | SelectBy(scope, label, index, maybeFeature);

Node scope := Lift(source, label, k);

Optional<Feature> maybeFeature := Feature(index, label, token) | AnyFeature();

string token := ConstToken(s) | VarToken(old, key) | ErrToken(old, index, label);

SyntaxNode newTree := New(tree);

PartialNode tree := Copy(ref) | Leaf(label, token) | Node(label, children) | ListNode(label, siblings);

IEnumerable<PartialNode> children := Child(tree) | Children(tree, children);

IEnumerable<PartialNode> siblings := Append(tree, siblings) | Prepend(tree, siblings)
                                   | Front(ref) | Tail(ref) | Siblings(ref);

Optional<int> index;
Label label;
int k;
string s;
EnvKey key;