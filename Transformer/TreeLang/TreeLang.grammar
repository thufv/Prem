using System.Collections.Generic;

using Prem.Util;
using Prem.Transformer.TreeLang;

using semantics Prem.Transformer.TreeLang.Semantics;
using learners Prem.Transformer.TreeLang.WitnessFunctions;

language TreeLang;

@complete feature double Score = Prem.Transformer.TreeLang.RankingScore;

@input TInput source;

@start SyntaxNode program := Ins(target, k, newTree) | Del(target) | Upd(target, newTree);

SyntaxNode newTree := New(tree);

PartialNode tree := Copy(target) | Leaf(label, token) | Tree(label, children);

IEnumerable<PartialNode> children := Child(tree) | Children(tree, children);

SyntaxNode target := Just(source) 
                   | ancestor
                   | Find(ancestor, label, tok, rel);

Optional.Option<string> tok := Match(token) | Any();

SyntaxNode ancestor := AbsAnc(source, k) | RelAnc(source, label, k);

Func<SyntaxNode, bool> rel := Rel(locator, label, token) | NoRel();

string token := Const(s) | Var(source, i) | FindE(source, rel);

int k;
int i;
string s;

Label label;
SiblingLocator locator;