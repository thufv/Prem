using System.Collections.Generic;
using Microsoft.ProgramSynthesis.Utils;

using Prem.Util;
using Prem.Transformer.TreeLang;

using semantics Prem.Transformer.TreeLang.Semantics;
using learners Prem.Transformer.TreeLang.WitnessFunctions;

language TreeLang;

@complete feature double Score = Prem.Transformer.TreeLang.RankingScore;

@input TInput source;

@start SyntaxNode program := Ins(target, k, newTree) | Del(target) | Upd(target, newTree);

SyntaxNode newTree := New(tree);

PartialNode tree := Copy(nodeRef) | Leaf(label, token) | Tree(label, children);

IEnumerable<PartialNode> children := Child(tree) | Children(tree, children);

SyntaxNode target := Just(source)
                   | nodeRef;
                   
SyntaxNode nodeRef := ancestor
                    | Find(ancestor, label, k)
                    | FindRel(ancestor, label, locator, siblingFeature);

SyntaxNode ancestor := AbsAnc(source, k) | RelAnc(source, label, k);

Record<Label, string>? siblingFeature := Feature(label, token) = Pair(label, token);

string token := Const(s) | Var(source, i) | FindToken(source, k, label, k);

int k;
int i;
string s;

Label label;
SiblingLocator locator;